unit CheckField;

interface

uses
  System.SysUtils, System.Classes, FMX.Types, FMX.Controls, System.Types,
  FMX.Objects, FMX.StdCtrls, System.UITypes, FMX.Graphics, FMX.Dialogs, System.Math,
  System.Math.Vectors, FMX.Edit, FMX.Layouts, FMX.Effects, ColorClass;

type
  TCheckField = class(TControl)
  private
    { Private declarations }
  protected
    { Protected declarations }
    FPointerOnMouseEnter: TNotifyEvent;
    FPointerOnMouseExit: TNotifyEvent;
    FPointerOnClick: TNotifyEvent;
    FPointerOnChange: TNotifyEvent;

    FText: TLabel;
    FLayoutIcon: TLayout;
    FUncheckedIcon: TPath;
    FCheckedIcon: TPath;
    FCircleSelect: TCircle;
    FLayout: TLayout;

    FBackupFontColor: TAlphaColor;
    FBackupUncheckedIconColor: TAlphaColor;

    FDisableOnCheck: TCheckField;

    procedure Paint; override;
    procedure Resize; override;
    procedure Painting; override;

    procedure OnCheckFieldClick(Sender: TObject);
    procedure OnCheckFieldMouseEnter(Sender: TObject);
    procedure OnCheckFieldMouseExit(Sender: TObject);

    procedure SetFCircleSelectColor(const Value: TAlphaColor);
    function GetFTag: NativeInt;
    procedure SetFTag(const Value: NativeInt);
    function GetFOnClick: TNotifyEvent;
    procedure SetFOnClick(const Value: TNotifyEvent);
    function GetFButtonClass: TColorClass;
    procedure SetFButtonClass(const Value: TColorClass);
    function GetFCheckedIconColor: TAlphaColor;
    function GetFUncheckedIcon: TPathData;
    function GetFUncheckedIconColor: TAlphaColor;
    procedure SetFCheckedIconColor(const Value: TAlphaColor);
    procedure SetFUncheckedIcon(const Value: TPathData);
    procedure SetFUncheckedIconColor(const Value: TAlphaColor);
    function GetFCheckedIcon: TPathData;
    procedure SetFCheckedIcon(const Value: TPathData);
    function GetFIconSize: Single;
    procedure SetFIconSize(const Value: Single);
    function GetFText: String;
    procedure SetFText(const Value: String);
    function GetFTextSettings: TTextSettings;
    procedure SetFTextSettings(const Value: TTextSettings);
    function GetFIsChecked: Boolean;
    procedure SetFIsChecked(const Value: Boolean);
    function GetFOnChange: TNotifyEvent;
    procedure SetFOnChange(const Value: TNotifyEvent);
  public
    { Public declarations }
    constructor Create(AOwner: TComponent); override;
    destructor Destroy; override;

    function ValidateIsChecked(): Boolean;
    procedure HideEffectValidation();
  published
    { Published declarations }
    property Cursor;
    property Align;
    property Anchors;
    property Enabled;
    property Height;
    property Opacity;
    property Visible;
    property Width;
    property Size;
    property Scale;
    property Margins;
    property Position;
    property RotationAngle;
    property RotationCenter;
    property HitTest;

    { Additional properties }
    property ButtonClass: TColorClass read GetFButtonClass write SetFButtonClass;
    property Text: String read GetFText write SetFText;
    property TextSettings: TTextSettings read GetFTextSettings write SetFTextSettings;
    property IsChecked: Boolean read GetFIsChecked write SetFIsChecked;
    property UncheckedIcon: TPathData read GetFUncheckedIcon write SetFUncheckedIcon;
    property CheckedIcon: TPathData read GetFCheckedIcon write SetFCheckedIcon;
    property UncheckedIconColor: TAlphaColor read GetFUncheckedIconColor write SetFUncheckedIconColor;
    property CheckedIconColor: TAlphaColor read GetFCheckedIconColor write SetFCheckedIconColor;
    property IconSize: Single read GetFIconSize write SetFIconSize;
    property Tag: NativeInt read GetFTag write SetFTag;
    property DisableOnCheck: TCheckField read FDisableOnCheck write FDisableOnCheck;

    { Events }
    property OnPainting;
    property OnPaint;
    property OnResize;

    { Mouse events }
    property OnChange: TNotifyEvent read GetFOnChange write SetFOnChange;
    property OnClick: TNotifyEvent read GetFOnClick write SetFOnClick;
    property OnDblClick;
    property OnKeyDown;
    property OnKeyUp;
    property OnMouseDown;
    property OnMouseUp;
    property OnMouseWheel;
    property OnMouseMove;
    property OnMouseEnter;
    property OnMouseLeave;
    property OnEnter;
    property OnExit;
  end;

procedure Register;

implementation

procedure Register;
begin
  RegisterComponents('Componentes Customizados', [TCheckField]);
end;

{ TCheckField }

constructor TCheckField.Create(AOwner: TComponent);
begin
  inherited;
  Self.Width := 250;
  Self.Height := 30;

  FText := TLabel.Create(Self);
  Self.AddObject(FText);
  FText.Align := TAlignLayout.Client;
  FText.HitTest := False;
  FText.Margins.Left := 10;
  FText.StyledSettings := [];
  FText.TextSettings.Font.Size := 14;
  FText.TextSettings.Font.Family := 'SF Pro Display';
  FText.TextSettings.FontColor := TAlphaColor($FF323232);
  FBackupFontColor := TAlphaColor($FF323232);
  FText.SetSubComponent(True);
  FText.Stored := False;

  FLayoutIcon := TLayout.Create(Self);
  Self.AddObject(FLayoutIcon);
  FLayoutIcon.Align := TAlignLayout.Left;
  FLayoutIcon.HitTest := False;
  FLayoutIcon.SetSubComponent(True);
  FLayoutIcon.Stored := False;
  FLayoutIcon.Width := 17;

  FCircleSelect := TCircle.Create(Self);
  FLayoutIcon.AddObject(FCircleSelect);
  FCircleSelect.Align := TAlignLayout.Center;
  FCircleSelect.HitTest := False;
  FCircleSelect.SetSubComponent(True);
  FCircleSelect.Stored := False;
  FCircleSelect.Stroke.Kind := TBrushKind.None;
  FCircleSelect.Visible := True;
  FCircleSelect.Opacity := 0;
  FCircleSelect.Width := (Self.Height) * 1.4;
  FCircleSelect.Height := (Self.Height) * 1.4;
  SetFCircleSelectColor(TRANSPARENT_PRIMARY_COLOR);
  FCircleSelect.SendToBack;

  FUncheckedIcon := TPath.Create(Self);
  FLayoutIcon.AddObject(FUncheckedIcon);
  FUncheckedIcon.SetSubComponent(True);
  FUncheckedIcon.Stored := False;
  FUncheckedIcon.Align := TAlignLayout.Contents;
  FUncheckedIcon.WrapMode := TPathWrapMode.Fit;
  FUncheckedIcon.Fill.Color := TAlphaColor($FF757575);
  FBackupUncheckedIconColor := TAlphaColor($FF757575);
  FUncheckedIcon.Stroke.Kind := TBrushKind.None;
  FUncheckedIcon.HitTest := False;
  FUncheckedIcon.Data.Data :=
    'M19,3H5C3.89,3 3,3.89 3,5V19A2,2 0 0,0 5,21H19A2,2 0 0,0 21,19V5C21,3.89 20.1,3 19,3M19,5V19H5V5H19Z';

  FCheckedIcon := TPath.Create(Self);
  FLayoutIcon.AddObject(FCheckedIcon);
  FCheckedIcon.Align := TAlignLayout.Contents;
  FCheckedIcon.WrapMode := TPathWrapMode.Fit;
  FCheckedIcon.Fill.Color := TAlphaColor($FF1867C0);
  FCheckedIcon.Stroke.Kind := TBrushKind.None;
  FCheckedIcon.HitTest := False;
  FCheckedIcon.Visible := False;
  FCheckedIcon.Data.Data :=
    'M10,17L5,12L6.41,10.58L10,14.17L17.59,6.58L19,8M19,3H5C3.89,3 3,3.89 3,5V19A2,2 0 0,0 5,21H19A2,2 0 0,0 21,19V5C21,3.89 20.1,3 19,3Z';
  FCheckedIcon.BringToFront;

  FLayout := TLayout.Create(Self);
  Self.AddObject(FLayout);
  FLayout.Align := TAlignLayout.Contents;
  FLayout.HitTest := True;
  FLayout.SetSubComponent(True);
  FLayout.Stored := False;
  FLayout.Cursor := crHandPoint;
  FLayout.OnClick := OnCheckFieldClick;
  FLayout.OnMouseEnter := OnCheckFieldMouseEnter;
  FLayout.OnMouseLeave := OnCheckFieldMouseExit;
  FLayout.BringToFront;
end;

destructor TCheckField.Destroy;
begin
  if Assigned(FLayout) then
    FLayout.Free;
  if Assigned(FCheckedIcon) then
    FCheckedIcon.Free;
  if Assigned(FUncheckedIcon) then
    FUncheckedIcon.Free;
  if Assigned(FText) then
    FText.Free;
  inherited;
end;

procedure TCheckField.OnCheckFieldClick(Sender: TObject);
var
  CircleEffect: TCircle;
begin
  CircleEffect := TCircle.Create(Self);
  CircleEffect.HitTest := False;
  CircleEffect.Parent := FLayoutIcon;
  CircleEffect.Align := TAlignLayout.Center;
  CircleEffect.Stroke.Kind := TBrushKind.None;
  CircleEffect.Fill.Color := FCheckedIcon.Fill.Color;
  CircleEffect.SendToBack;

  CircleEffect.Height := 0;
  CircleEffect.Width := 0;
  CircleEffect.Opacity := 0.6;
  CircleEffect.AnimateFloat('Height', FCircleSelect.Height, 0.4, TAnimationType.Out, TInterpolationType.Circular);
  CircleEffect.AnimateFloat('Width', FCircleSelect.Width, 0.4, TAnimationType.Out, TInterpolationType.Circular);
  CircleEffect.AnimateFloat('Opacity', 0, 0.5);

  if not FCheckedIcon.Visible then
  begin
    FText.TextSettings.FontColor := FBackupFontColor;
    FUncheckedIcon.Fill.Color := FBackupUncheckedIconColor;

    FCheckedIcon.Visible := True;
    FUncheckedIcon.Visible := False;
    if Assigned(FDisableOnCheck) then
      FDisableOnCheck.IsChecked := False;
  end
  else
  begin
    FUncheckedIcon.Visible := True;
    FCheckedIcon.Visible := False;
  end;

  if Assigned(FPointerOnClick) then
    FPointerOnClick(Sender);

  if Assigned(FPointerOnChange) then
    FPointerOnChange(Sender);
end;

procedure TCheckField.OnCheckFieldMouseEnter(Sender: TObject);
begin
  FCircleSelect.AnimateFloat('Opacity', 1, 0.2, TAnimationType.InOut);

  if Assigned(FPointerOnMouseEnter) then
    FPointerOnMouseEnter(Sender);
end;

procedure TCheckField.OnCheckFieldMouseExit(Sender: TObject);
begin
  FCircleSelect.AnimateFloat('Opacity', 0, 0.2, TAnimationType.InOut);

  if Assigned(FPointerOnMouseExit) then
    FPointerOnMouseExit(Sender);
end;

procedure TCheckField.Paint;
begin
  inherited;
end;

procedure TCheckField.Painting;
begin
  inherited;
end;

procedure TCheckField.Resize;
begin
  inherited;
end;

function TCheckField.GetFCheckedIconColor: TAlphaColor;
begin
  Result := FCheckedIcon.Fill.Color;
end;

function TCheckField.GetFIconSize: Single;
begin
  Result := FLayoutIcon.Width;
end;

function TCheckField.GetFIsChecked: Boolean;
begin
  Result := FCheckedIcon.Visible;
end;

function TCheckField.GetFOnChange: TNotifyEvent;
begin
  Result := FPointerOnChange;
end;

function TCheckField.GetFOnClick: TNotifyEvent;
begin
  Result := FPointerOnClick;
end;

function TCheckField.GetFTag: NativeInt;
begin
  Result := TControl(Self).Tag;
end;

function TCheckField.GetFText: String;
begin
  Result := FText.Text;
end;

function TCheckField.GetFTextSettings: TTextSettings;
begin
  Result := FText.TextSettings;
end;

function TCheckField.GetFUncheckedIcon: TPathData;
begin
  Result := FUncheckedIcon.Data;
end;

function TCheckField.GetFUncheckedIconColor: TAlphaColor;
begin
  Result := FUncheckedIcon.Fill.Color;
end;

function TCheckField.GetFButtonClass: TColorClass;
begin
  if FCheckedIcon.Fill.Color = SOLID_PRIMARY_COLOR then
    Result := TColorClass.Primary
  else if FCheckedIcon.Fill.Color = SOLID_SECONDARY_COLOR then
    Result := TColorClass.Secondary
  else if FCheckedIcon.Fill.Color = SOLID_ERROR_COLOR then
    Result := TColorClass.Error
  else if FCheckedIcon.Fill.Color = SOLID_WARNING_COLOR then
    Result := TColorClass.Warning
  else if FCheckedIcon.Fill.Color = SOLID_SUCCESS_COLOR then
    Result := TColorClass.Success
  else if FCheckedIcon.Fill.Color = SOLID_BLACK then
    Result := TColorClass.Normal
  else
    Result := TColorClass.Custom;
end;

function TCheckField.GetFCheckedIcon: TPathData;
begin
  Result := FCheckedIcon.Data;
end;

procedure TCheckField.SetFButtonClass(const Value: TColorClass);
begin
  if Value = TColorClass.Primary then
  begin
    SetFCheckedIconColor(SOLID_PRIMARY_COLOR);
    SetFCircleSelectColor(TRANSPARENT_PRIMARY_COLOR);
  end
  else if Value = TColorClass.Secondary then
  begin
    SetFCheckedIconColor(SOLID_SECONDARY_COLOR);
    SetFCircleSelectColor(TRANSPARENT_SECONDARY_COLOR);
  end
  else if Value = TColorClass.Error then
  begin
    SetFCheckedIconColor(SOLID_ERROR_COLOR);
    SetFCircleSelectColor(TRANSPARENT_ERROR_COLOR);
  end
  else if Value = TColorClass.Warning then
  begin
    SetFCheckedIconColor(SOLID_WARNING_COLOR);
    SetFCircleSelectColor(TRANSPARENT_WARNING_COLOR);
  end
  else if Value = TColorClass.Normal then
  begin
    SetFCheckedIconColor(SOLID_BLACK);
    SetFCircleSelectColor(TRANSPARENT_BLACK);
  end
  else if Value = TColorClass.Success then
  begin
    SetFCheckedIconColor(SOLID_SUCCESS_COLOR);
    SetFCircleSelectColor(TRANSPARENT_SUCCESS_COLOR);
  end
  else
  begin
    SetFCheckedIconColor(TAlphaColor($FF323232));
    SetFCircleSelectColor(TAlphaColor($1E323232));
  end;
end;

procedure TCheckField.SetFCheckedIcon(const Value: TPathData);
begin
  FCheckedIcon.Data := Value;
end;

procedure TCheckField.SetFCheckedIconColor(const Value: TAlphaColor);
begin
  FCheckedIcon.Fill.Color := Value;
end;

procedure TCheckField.SetFCircleSelectColor(const Value: TAlphaColor);
begin
  FCircleSelect.Fill.Color := Value;
end;

procedure TCheckField.SetFIconSize(const Value: Single);
begin
  FLayoutIcon.Width := Value;
  FLayoutIcon.Height := Value;
  Self.Height := (FLayoutIcon.Width) * 1.4;
end;

procedure TCheckField.SetFIsChecked(const Value: Boolean);
begin
  FCheckedIcon.Visible := Value;
  FUncheckedIcon.Visible := not Value;
end;

procedure TCheckField.SetFOnChange(const Value: TNotifyEvent);
begin
  FPointerOnChange := Value;
end;

procedure TCheckField.SetFOnClick(const Value: TNotifyEvent);
begin
  FPointerOnClick := Value;
end;

procedure TCheckField.SetFTag(const Value: NativeInt);
begin
  FLayout.Tag := Value;
  TControl(Self).Tag := Value;
end;

procedure TCheckField.SetFText(const Value: String);
begin
  FText.Text := Value;
end;

procedure TCheckField.SetFTextSettings(const Value: TTextSettings);
begin
  FText.TextSettings := Value;
  FBackupFontColor := Value.FontColor;
end;

procedure TCheckField.SetFUncheckedIcon(const Value: TPathData);
begin
  FUncheckedIcon.Data := Value;
end;

procedure TCheckField.SetFUncheckedIconColor(const Value: TAlphaColor);
begin
  FUncheckedIcon.Fill.Color := Value;
  FBackupUncheckedIconColor := Value;
end;

function TCheckField.ValidateIsChecked: Boolean;
begin
  if not Self.IsChecked and (FUncheckedIcon.Fill.Color <> SOLID_ERROR_COLOR) then
  begin
    FBackupFontColor := FText.TextSettings.FontColor;
    FBackupUncheckedIconColor := FUncheckedIcon.Fill.Color;

    FText.TextSettings.FontColor := SOLID_ERROR_COLOR;
    FUncheckedIcon.Fill.Color := SOLID_ERROR_COLOR;
  end;
  Result := Self.IsChecked;
end;

procedure TCheckField.HideEffectValidation;
begin
  if FUncheckedIcon.Fill.Color = SOLID_ERROR_COLOR then
  begin
    FText.TextSettings.FontColor := FBackupFontColor;
    FUncheckedIcon.Fill.Color := FBackupUncheckedIconColor;
  end;
end;

end.
