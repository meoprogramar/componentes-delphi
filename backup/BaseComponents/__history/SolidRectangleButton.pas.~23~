unit SolidRectangleButton;

interface

uses
  System.SysUtils, System.Classes, FMX.Types, FMX.Controls, System.Types,
  FMX.Objects, FMX.StdCtrls, System.UITypes, FMX.Graphics, FMX.Dialogs, System.Math,
  System.Math.Vectors, FMX.Edit, FMX.Layouts, FMX.Effects, TransparentRectangleButton;

const
  PRIMARY_COLOR: TAlphaColor = ($FF1867C0);
  ERROR_COLOR: TAlphaColor = ($FFFF5252);
  NORMAL_COLOR: TAlphaColor = ($FFF5F5F5);
  SECONDARY_COLOR: TAlphaColor = ($FF5CBBF6);
  WARNING_COLOR: TAlphaColor = ($FFFB8C00);
  SUCCESS_COLOR: TAlphaColor = ($FF4CAF50);

type
  TSolidRectangleButton = class(TTransparentRectangleButton)
  private
    { Private declarations }

    function GetFElevation: Boolean;
    procedure SetFElevation(const Value: Boolean);
    function GetFDark: Boolean;
    procedure SetFDark(const Value: Boolean);
  protected
    { Protected declarations }
    FBackgroundButton: TRectangle;
    FShadow: TShadowEffect;
    FContrastColor: TAlphaColor;

    procedure Paint; override;
    procedure Resize; override;
    procedure Painting; override;

    procedure BackgroundOnClick(Sender: TObject); override;
    procedure BackgroundOnEnter(Sender: TObject); override;
    procedure BackgroundOnExit(Sender: TObject); override;
    function GetFButtonClass: TColorClass; override;
    procedure SetFButtonClass(const Value: TColorClass); override;
    function GetFBackgroudColor: TAlphaColor; override;
    procedure SetFBackgroudColor(const Value: TAlphaColor); override;
  public
    { Public declarations }
    constructor Create(AOwner: TComponent); override;
    destructor Destroy; override;
  published
    { Published declarations }
    { Additional properties }
    property Elevation: Boolean read GetFElevation write SetFElevation;
    property Dark: Boolean read GetFDark write SetFDark;
  end;

procedure Register;

implementation

procedure Register;
begin
  RegisterComponents('Componentes Customizados', [TSolidRectangleButton]);
end;

{ TSolidRectangleButton }

procedure TSolidRectangleButton.BackgroundOnClick(Sender: TObject);
var
  CircleEffect: TCircle;
begin
  CircleEffect := TCircle.Create(Self);
  CircleEffect.HitTest := False;
  CircleEffect.Parent := FBackgroundButton;
  CircleEffect.Align := TAlignLayout.Center;
  CircleEffect.Stroke.Kind := TBrushKind.None;
  CircleEffect.Fill.Color := FContrastColor;
  CircleEffect.SendToBack;
  FBackgroundButton.ClipChildren := True;

  CircleEffect.Height := 0;
  CircleEffect.Width := 0;
  CircleEffect.Opacity := 0.2;
  CircleEffect.AnimateFloat('Height', FBackgroundButton.Width + (FBackgroundButton.Width * 0.2), 0.4,
    TAnimationType.Out, TInterpolationType.Circular);
  CircleEffect.AnimateFloat('Width', FBackgroundButton.Width + (FBackgroundButton.Width * 0.2), 0.4, TAnimationType.Out,
    TInterpolationType.Circular);
  CircleEffect.AnimateFloat('Opacity', 0, 0.4);

  if Assigned(FPointerOnClick) then
    FPointerOnClick(Sender);
end;

procedure TSolidRectangleButton.BackgroundOnEnter(Sender: TObject);
begin
  if Elevation then
  begin
    FShadow.AnimateFloat('Distance', 5, 0.2, TAnimationType.InOut);
    FShadow.AnimateFloat('Softness', 0.3, 0.2, TAnimationType.InOut);
  end;

  FBackgroundSelect.AnimateFloat('Opacity', 0.1, 0.1, TAnimationType.InOut);

  if Assigned(FPointerOnMouseEnter) then
    FPointerOnMouseEnter(Sender);
end;

procedure TSolidRectangleButton.BackgroundOnExit(Sender: TObject);
begin
  if Elevation then
  begin
    FShadow.AnimateFloat('Distance', 2, 0.2, TAnimationType.InOut);
    FShadow.AnimateFloat('Softness', 0.15, 0.2, TAnimationType.InOut);
  end;

  FBackgroundSelect.AnimateFloat('Opacity', 0, 0.1, TAnimationType.InOut);

  if Assigned(FPointerOnMouseExit) then
    FPointerOnMouseExit(Sender);
end;

constructor TSolidRectangleButton.Create(AOwner: TComponent);
begin
  inherited;

  FContrastColor := TAlphaColor($FFFFFFFF);

  FBackgroundButton := TRectangle.Create(Self);
  Self.AddObject(FBackgroundButton);
  FBackgroundButton.SetSubComponent(True);
  FBackgroundButton.Stored := False;
  FBackgroundButton.Align := TAlignLayout.Client;
  FBackgroundButton.Stroke.Kind := TBrushKind.None;
  FBackgroundButton.Fill.Color := TAlphaColor($FF1867C0);
  FBackgroundButton.XRadius := 4;
  FBackgroundButton.YRadius := 4;
  FBackgroundButton.OnMouseEnter := BackgroundOnEnter;
  FBackgroundButton.OnMouseLeave := BackgroundOnExit;
  FBackgroundButton.OnClick := BackgroundOnClick;
  FBackgroundButton.Cursor := crHandPoint;
  FBackgroundButton.SendToBack;

  FBackgroundSelect.Fill.Color := FContrastColor;

  FText.TextSettings.FontColor := FContrastColor;

  FShadow := TShadowEffect.Create(Self);
  Self.AddObject(FShadow);
  FShadow.Direction := 90;
  FShadow.Distance := 2;
  FShadow.Opacity := 0.3;
  FShadow.Softness := 0.15;
  FShadow.Enabled := True;
  FShadow.SetSubComponent(True);
  FShadow.Stored := False;
end;

destructor TSolidRectangleButton.Destroy;
begin
  if Assigned(FShadow) then
    FShadow.Free;
  if Assigned(FBackgroundButton) then
    FBackgroundButton.Free;
  inherited;
end;

function TSolidRectangleButton.GetFBackgroudColor: TAlphaColor;
begin
  inherited;
  if Assigned(FBackgroundButton) then
    Result := FBackgroundButton.Fill.Color;
end;

function TSolidRectangleButton.GetFButtonClass: TColorClass;
begin
  if FBackgroundButton.Fill.Color = PRIMARY_COLOR then
    Result := TColorClass.Primary
  else if FBackgroundButton.Fill.Color = SECONDARY_COLOR then
    Result := TColorClass.Secondary
  else if FBackgroundButton.Fill.Color = ERROR_COLOR then
    Result := TColorClass.Error
  else if FBackgroundButton.Fill.Color = WARNING_COLOR then
    Result := TColorClass.Warning
  else if FBackgroundButton.Fill.Color = SUCCESS_COLOR then
    Result := TColorClass.Success
  else if FBackgroundButton.Fill.Color = NORMAL_COLOR then
    Result := TColorClass.Normal
  else
    Result := TColorClass.Custom;
end;

function TSolidRectangleButton.GetFDark: Boolean;
begin
  if FContrastColor = TAlphaColor($FF323232) then
    Result := True
  else
    Result := False;
end;

function TSolidRectangleButton.GetFElevation: Boolean;
begin
  Result := FShadow.Enabled;
end;

procedure TSolidRectangleButton.Paint;
begin
  inherited;
  if FIcon.Data.Data.Equals('') then
  begin
    FIcon.Visible := False;
    FText.TextSettings.HorzAlign := TTextAlign.Center;
  end
  else
  begin
    FIcon.Visible := True;
    SetFIconPosition(GetFIconPosition);
  end;

  FText.TextSettings.FontColor := FContrastColor;
  FIcon.Fill.Color := FContrastColor;
end;

procedure TSolidRectangleButton.Painting;
begin
  inherited;
end;

procedure TSolidRectangleButton.Resize;
begin
  inherited;
end;

procedure TSolidRectangleButton.SetFBackgroudColor(const Value: TAlphaColor);
begin
  inherited;
  if Assigned(FBackgroundButton) then
    FBackgroundButton.Fill.Color := Value;
end;

procedure TSolidRectangleButton.SetFButtonClass(const Value: TColorClass);
begin
  if Value = TColorClass.Primary then
  begin
    FBackgroundButton.Fill.Color := PRIMARY_COLOR;
    SetFDark(False);
  end
  else if Value = TColorClass.Secondary then
  begin
    FBackgroundButton.Fill.Color := SECONDARY_COLOR;
    SetFDark(False);
  end
  else if Value = TColorClass.Error then
  begin
    FBackgroundButton.Fill.Color := ERROR_COLOR;
    SetFDark(False);
  end
  else if Value = TColorClass.Warning then
  begin
    FBackgroundButton.Fill.Color := WARNING_COLOR;
    SetFDark(False);
  end
  else if Value = TColorClass.Normal then
  begin
    FBackgroundButton.Fill.Color := NORMAL_COLOR;
    SetFDark(True);
  end
  else if Value = TColorClass.Success then
  begin
    FBackgroundButton.Fill.Color := SUCCESS_COLOR;
    SetFDark(False);
  end
  else
  begin
    FBackgroundButton.Fill.Color := TAlphaColor($FF323232);
    SetFDark(False);
  end;
end;

procedure TSolidRectangleButton.SetFDark(const Value: Boolean);
begin
  if Value then
    FContrastColor := TAlphaColor($FF323232)
  else
    FContrastColor := TAlphaColor($FFFFFFFF);
end;

procedure TSolidRectangleButton.SetFElevation(const Value: Boolean);
begin
  FShadow.Enabled := Value;
end;

end.
